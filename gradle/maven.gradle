apply plugin: 'maven'

ext.optionalDeps = []
ext.providedDeps = []

ext.optional = { optionalDeps << it }
ext.provided = { providedDeps << it }

install {
  repositories.mavenInstaller {
    customizePom(pom, project)
  }
}

def customizePom(pom, gradleProject) {
  pom.whenConfigured { generatedPom ->
    // respect 'optional' and 'provided' dependencies
    gradleProject.optionalDeps.each { dep ->
      generatedPom.dependencies.find { it.artifactId == dep.name }?.optional = true
    }
    gradleProject.providedDeps.each { dep ->
      generatedPom.dependencies.find { it.artifactId == dep.name }?.scope = 'provided'
    }

    // eliminate test-scoped dependencies (no need in maven central poms)
    generatedPom.dependencies.removeAll { dep ->
      dep.scope == 'test'
    }

    // add all items necessary for maven central publication
    generatedPom.project {
      name = "monitoring"
      description = "ncsoft platform monitoring system"
      url = ''
      organization {
        name = 'infraplatform'
        url = ''
      }
      licenses {
        license {
          name 'The NCSOFT Software License, Version 1.0'
          url ''
          distribution 'repo'
        }
      }
      scm {
        url = ''
        connection = ''
        developerConnection = ''
      }
      developers {
        developer {
          id = ''
          name = ''
          email = ''
        }
      }
    }
  }
}